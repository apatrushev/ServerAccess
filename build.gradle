plugins {
    id 'info.solidsoft.pitest' version '1.4.5'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.sonarqube' version '2.8'
}

ext {
    swtVersion = '4.6.1'
    currentVersion = detectVersion()
}

def detectVersion() {
    try {
        def rawVersion = 'git describe'.execute().text.trim()
        def extractedVersion = rawVersion =~ /serveraccess-([0-9.]*(-[0-9]+)?)/
        return extractedVersion[0][1]
    } catch (e) {
        return 'devel'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'ru.naumen'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven {url 'http://maven-eclipse.github.io/maven'}
    }

    jar {
        manifest {
            attributes 'Main-Class': 'ru.naumen.servacc.ui.Main'
        }
    }

    shadowJar {
        baseName = rootProject.name
    }

    tasks.shadowJar.dependsOn 'prepareRelease'

    task prepareRelease(dependsOn: build) {
    }
}

task fullBuild(dependsOn: [':lin64:fullBuild', ':win32:fullBuild', ':win64:fullBuild', ':macosx:fullBuild']) {}
